!
! Program lifetime: Computes the survival periods of bonds (hbonds, solvation, or other
!                   bond computed from one of the other programs, from the list of bonds
!                   per frame that those programs may output.
!
!    Run with:  lifetime inputfile.dat > output.dat
!
!    The inputfile.dat is the file generated by some of the other programs that are 
!    compatible with this one, and the output.dat file is the file that will contain
!    the data of the number of bonds per survival period.
!  
!    The input file has the form:
!
!       FRAME   I   J  (a list of three integers, being the the first the simulation
!                       frame and I and J the indexes of the atoms of the bond)
!
!    The output has four rows:
!
!       1: The time lapse between frames.
!       2: The normalized probability of finding a hbond at time t if it was found
!          at time zero.
!       3: The number of hydrogen bonds, per frame, that are found at time t if
!          were found at time zero (the first point is simply the average number
!          of hydrogen bonds).
!       4: The total number of hydrogen bonds, without any normalization
!
! L. Martinez
! Institute of Chemistry - State University of Campinas
! Aug 26, 2014
!
! Version 16.323
!

program lifetime

  implicit none
  integer :: status, iframe, i, j, k, imax, jmax, idata, isurvive, &
             ndata, maxsurvival, lastframe, nframes, deltaframe
  integer, allocatable :: histogram(:)
  integer, allocatable :: frame(:), firstij(:,:), inext(:)
  character (len=200) :: inputfile, record

  if ( iargc() /= 1 ) then
    write(*,*) ' Program lifetime '
    write(*,*) ' Run with: lifetime inputfile.dat > output.dat '
    stop
  end if

  call getarg(1,inputfile)

  open(10,file=inputfile,status='old',action='read')

  ! Read input file

  imax = 0
  jmax = 0
  ndata = 0
  lastframe = 0
  nframes = 0
  deltaframe = 0
  do 
    read(10,"( a200 )",iostat=status) record
    if ( status /= 0 ) exit
    if ( record(1:1) == "#" ) cycle
    read(record,*,iostat=status) iframe, i, j
    if ( status /= 0 ) then
      write(*,*) ' ERROR: Failed reading data', ndata + 1
      stop
    end if
    if ( iframe /= lastframe ) then
      nframes = nframes + 1
      deltaframe = iframe - lastframe
      lastframe = iframe
    end if
    ndata = ndata + 1
    if ( i > j ) then
      imax = max0(imax,i)
      jmax = max0(jmax,j)
    else if ( i < j ) then
      imax = max0(imax,j)
      jmax = max0(jmax,i)
    else
      write(*,*) ' ERROR: i = j. This should never happen. Contact the developer. '
      stop
    end if 
  end do

  ! Output data file information

  write(*,"( '#',/,'# Lifetime: Compute histogram of lifetimes.',/,'#' )")
  write(*,"( '# File name: ', a )") inputfile(1:len(trim(inputfile)))
  write(*,"( '# Number of frames: ', i10 )") nframes
  write(*,"( '# Delta between frames: ', i10 )") deltaframe
  write(*,"( '# Number of data points: ', i10 )") ndata
  write(*,"( '# Maximum index i: ', i10 )") imax
  write(*,"( '# Maximum index j: ', i10 )") jmax

  allocate(frame(ndata), firstij(imax,jmax), inext(ndata), histogram(nframes))

  ! Initialize the linked list array

  do j = 1, jmax
    do i = j + 1, imax
      firstij(i,j) = 0
    end do
  end do

  ! Read the data again, now filling up the linked lists with the data

  rewind(10)
  idata = 0
  do 
    read(10,"( a200 )",iostat=status) record
    if ( status /= 0 ) exit
    if ( record(1:1) == "#" ) cycle
    idata = idata + 1
    read(record,*) iframe, i, j
    if ( i > j ) then
      inext(idata) = firstij(i,j)
      firstij(i,j) = idata
      frame(idata) = iframe
    else 
      inext(idata) = firstij(j,i)
      firstij(j,i) = idata
      frame(idata) = iframe
    end if
  end do
  close(10)

  ! Compute the survival times 

  do i = 1, nframes
    histogram(i) = 0
  end do

  do j = 1, jmax
    do i = j + 1, imax
      idata = firstij(i,j)
      if ( idata == 0 ) cycle
      isurvive = 1
      do 

        if ( inext(idata) == 0 ) then
          do k = 1, isurvive
            histogram(k) = histogram(k) + 1
          end do
          exit
        end if

        if ( frame(idata) - frame(inext(idata)) == deltaframe ) then
          do k = 1, isurvive
            histogram(k) = histogram(k) + 1
          end do
          isurvive = isurvive + 1
        else
          do k = 1, isurvive
            histogram(k) = histogram(k) + 1
          end do
          isurvive = 1
        end if
        idata = inext(idata)

      end do
    end do
  end do

  ! Output the histogram up to the maximum survival time
 
  maxsurvival = 0
  do i = 1, nframes
    if ( histogram(i) > 0 ) maxsurvival = i 
  end do

  write(*,"( '#',/,'#  Survival interval', t25,' Normalized average ',&
                   t49,' Average Number of bonds', t78, ' Number of Bonds ')")
  do i = 1, maxsurvival
    j = (i-1)*deltaframe
    write(*,"( i20, t27, e12.5, t53, f12.5, t68, i20 )") j, real(histogram(i))/real(histogram(1)),&
                                                            real(histogram(i))/real(nframes), histogram(i)
  end do

  deallocate(frame, firstij, inext, histogram)
end program lifetime
